# Maintainer: chrzrdx <chrzrdx at gmail dot com>

pkgname='st-git'
_pkgname='st'
appname='st'
conflicts=(${appname})
provides=(${appname})
pkgver=20160603.528241a
pkgrel=1
pkgdesc='Simple virtual terminal emulator for X'
url='http://git.suckless.org/st/'
arch=('i686' 'x86_64')
license=('MIT')
depends=('libxft')
makedepends=('ncurses' 'libxext' 'git')
source=('git://git.suckless.org/st'
        # 'st-no_bold_colors-git-20160616.diff'
        'st-gruvbox-dark-git-20160617.diff'
        'st-scrollback-git-20160617.diff'
        'st-scrollback-mouse-git-20160617.diff'
        'st-scrollback-mouse-altscreen-git-20160617.diff'
        'st-clipboard-git-20160617.diff'
        'st-spoiler-git-20160617.diff'
        # 'st-delkey-git-20160617.diff'
       )

md5sums=('SKIP'
        # 'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        # 'SKIP'
        )

provides=("${_pkgname}")
conflicts=("${_pkgname}")

pkgver() {
	cd "${srcdir}/${_pkgname}"
	git log -1 --format='%cd.%h' --date=short | tr -d -
}

prepare() {
	local file
	cd "${srcdir}/${_pkgname}"
	for file in "${source[@]}"; do
		if [[ "$file" == "config.h" ]]; then
			# add config.h if present in source array
			# Note: this supersedes the above sed to config.def.h
			cp "$srcdir/$file" .
			continue
		elif [[ "$file" == *.diff ]]; then
			# add all patches present in source array
			patch -Np1 <"$srcdir/$file"
		fi
	done
	sed \
		-e '/char font/s/= .*/= "Tamsyn:pixelsize=16";/' \
		-e '/char shell/s/= .*/= "\/bin\/zsh";/' \
		-e '/char worddelimiters/s/= .*/= " '"'"'`\\\"()[]{}<>|";/' \
		-e '/int tabspaces/s/= .*/= 4;/' \
		-i config.def.h
    # sed -e 's/define histsize.*$/define histsize 65000/g' -i config.def.h
	sed \
		-e 's/CPPFLAGS =/CPPFLAGS +=/g' \
		-e 's/CFLAGS =/CFLAGS +=/g' \
		-e 's/LDFLAGS =/LDFLAGS +=/g' \
		-e 's/_BSD_SOURCE/_DEFAULT_SOURCE/' \
		-i config.mk
	sed '/@tic/d' -i Makefile
}

build() {
	cd "${srcdir}/${_pkgname}"
	make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
	cd "${srcdir}/${_pkgname}"
	make PREFIX=/usr DESTDIR="${pkgdir}" install
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
}
